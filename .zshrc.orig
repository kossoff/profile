#!/usr/local/bin/zsh

# Позволяем разворачивать сокращенный ввод, к примеру cd /u/sh в /usr/share
autoload -U compinit && compinit

# загружаем список цветов
autoload -U colors && colors

# автодополнение команд с выбором в виде меню
zmodload zsh/complist
zstyle ':completion:*' menu yes select

# Use hard limits, except for a smaller stack and no core dumps
unlimit
limit stack 8192
limit core 0
limit -s

# rw-r--r-- for new files
umask 022

# prompt (if running screen, show window #)
if [ x$WINDOW != x ]; then
    export PS1="[%{$fg[green]%}%n%{$reset_color%}@%{$fg[magenta]%}%m:%:$WINDOW:%:%{$fg[red]%}%~ %{$reset_color%}]%% "
else
    export PS1="[%{$fg[green]%}%n%{$reset_color%}@%{$fg[magenta]%}%m:%{$fg[red]%}%~ %{$reset_color%}]%% "
fi


# Some functions, like _apt and _dpkg, are very slow.
#You can use a cache in order to proxy the list of results (like the list of available debian packages)
#Use a cache:
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.cache/zsh

# for directory listing
zstyle ':completion:*' completer _expand _complete _ignored
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt '%SAt %p: Hit TAB for more, or the character to insert%s'
zstyle ':completion:*' max-errors 1
zstyle ':completion:*' menu select=long-list select=0
zstyle ':completion:*' select-prompt '%SScrolling active: current selection at %p%s'
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true
zstyle :compinstall filename '/home/tony/.zshrc'
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# for kill
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:kill:*' force-list always
zstyle ':completion:*:*:kill:*:processes' list-colors "=(#b) #([0-9]#)*=$color[cyan]=$color[red]"

# ignore completion functions (until the _ignored completer)
zstyle ':completion:*:functions' ignored-patterns '_*'
zstyle ':completion:*:*:*:users' ignored-patterns \
	adm apache bin daemon games gdm halt ident junkbust lp mail mailnull \
	named news nfsnobody nobody nscd ntp operator pcap postgres radvd \
	rpc rpcuser rpm shutdown squid sshd sync uucp vcsa xfs avahi-autoipd\
	avahi backup messagebus beagleindex debian-tor dhcp dnsmasq fetchmail\
	firebird gnats haldaemon hplip irc klog list man cupsys postfix\
	proxy syslog www-data mldonkey sys snort debian-transmission libuuid\
	polkituser kernoops pdnsd privoxy saned usbmux git

# SSH Completion
zstyle ':completion:*:scp:*' tag-order files users 'hosts:-host hosts:-domain:domain hosts:-ipaddr"IP\ Address *'
zstyle ':completion:*:scp:*' group-order files all-files users hosts-domain hosts-host hosts-ipaddr
zstyle ':completion:*:ssh:*' tag-order users 'hosts:-host hosts:-domain:domain hosts:-ipaddr"IP\ Address *'
zstyle ':completion:*:ssh:*' group-order hosts-domain hosts-host users hosts-ipaddr
zstyle '*' single-ignored show

. $HOME/.alias
export HISTFILE=~/.zhistory

export HISTSIZE=10240
export SAVEHIST=10240

setopt SHARE_HISTORY
setopt APPEND_HISTORY
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_REDUCE_BLANKS
setopt HIST_NO_STORE

setopt AUTOCD BSD_ECHO
setopt CORRECT_ALL

export EDITOR=mcedit
export VISUAL=mcedit
export PAGER=less

LANG="ru_RU.UTF-8"
LC_CTYPE="ru_RU.UTF-8"
LC_COLLATE="POSIX"
LC_ALL="ru_RU.UTF-8"

# rigth prompt with clock
RPROMPT="  %{$fg_bold[black]%}%D{%d/%m/%y %H:%M:%S}%{${reset_color}%}"

HOME=/home/tony
export HOME

BLOCKSIZE=M
export BLOCKSIZE

# включает горячие клавиши из emacs, -v из vim
#bindkey -e
# разворачивание пути к команде
#bindkey 'вбить сочетание клавиш' expand-cmd-path
bindkey '^[[3~' delete-char

export LESS_TERMCAP_mb=$'\E[01;31m'
export LESS_TERMCAP_md=$'\E[01;31m'
export LESS_TERMCAP_me=$'\E[0m'
export LESS_TERMCAP_se=$'\E[0m'
export LESS_TERMCAP_so=$'\E[01;44;33m'
export LESS_TERMCAP_ue=$'\E[0m'
export LESS_TERMCAP_us=$'\E[01;32m'

# quick access to directories
# ~d6 in command line == cd /usr/local/www/drupal6
hash -d e=/etc

# {{{ OS settings
case `uname -s` in
    FreeBSD)
	export PATH=/usr/local/kde4/bin:/usr/local/kde4/sbin:/sbin:/bin:/usr/sbin:/usr/bin:/usr/local/sbin:/usr/local/bin:~/bin:~/.gem/ruby/1.8/bin

	export LSCOLORS="exgxfxcxcxdxdxhbadacec"
	alias ls="ls -G"
	
	# quick access to directories
	hash -d w=/usr/local/www
	hash -d d6=/usr/local/www/drupal6
	hash -d d7=/usr/local/www/drupal7
	hash -d ed=/etc/rc.d
	hash -d le=/usr/local/etc
	hash -d ld=/usr/local/etc/rc.d
    ;;

    Linux)
	export PATH=/sbin:/bin:/usr/sbin:/usr/bin:/usr/local/sbin:/usr/local/bin:~/bin:~/.gem/ruby/1.8/bin

	# Colored completion
	zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
	alias ls="ls --color=auto"
	
	# quick access to directories
	hash -d w=/var/www
	hash -d d6=/var/www/drupal6
	hash -d d7=/var/www/drupal7
	hash -d ed=/etc/init.d
    ;;

    Darwin)
	export HOME=/Users/admin
	export PATH=/usr/local/sbin:/usr/local/bin:~/.gem/ruby/1.8/bin:$PATH
    ;;
esac
# }}}

# for Tramp Mode in (Aqua|E)macs
[[ $TERM == "dumb" ]] && unsetopt zle && PS1='$ '

function precmd() {
    if [ "$DCOP_YAKUAKE_SESSION" ]
    then
	dcop yakuake DCOPInterface slotRenameSession $DCOP_YAKUAKE_SESSION "`pwd | sed "s,^$HOME,~,"`"
    fi
}

function title() {
    # escape ?%? chars in $1, make nonprintables visible
    a=${(V)1//\%/\%\%}

    # Truncate command, and join lines.
    a=$(print -Pn "%40>...>$a" | tr -d "\n")

    case $TERM in
	screen)
	    print -Pn "\e]2;$a @ $2\a" # plain xterm title
	    print -Pn "\ek$a\e\\" # screen title (in ^A?)
	    print -Pn "\e_$2 \e\\" # screen location
	;;

	*xterm*|rxvt|konsole|yakuake|(dt|k|E)term)
	    print -Pn "\e]2;$a @ $2\a" # plain xterm title
	;;
    esac
}

function preexec() {
    title "$1" "%m(%35<...<%~)"
}

function lcd() {
    cd "$1" && ls
}

name() {
    name=$1
    vared -c -p 'rename to: ' name
    command mv $1 $name
}

PATH=$PATH:$HOME/.rvm/bin # Add RVM to PATH for scripting
